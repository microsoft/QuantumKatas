// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT license.

//////////////////////////////////////////////////////////////////////
// This file contains reference solutions to all tasks.
// The tasks themselves can be found in Tasks.qs file.
// We recommend that you try to solve the tasks yourself first,
// but feel free to look up the solution if you get stuck.
//////////////////////////////////////////////////////////////////////

namespace Quantum.Kata.DeutschJozsaAlgorithm {
    
    open Microsoft.Quantum.Arrays;
    open Microsoft.Quantum.Diagnostics;
    open Microsoft.Quantum.Intrinsic;
    open Microsoft.Quantum.Canon;
    
    
    //////////////////////////////////////////////////////////////////
    // Part I. Oracles
    //////////////////////////////////////////////////////////////////
    
    // Task 1.1. f(x) = 0
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    operation Oracle_Zero_Reference (x : Qubit[], y : Qubit) : Unit is Adj {
        // Since f(x) = 0 for all values of x, |y ‚äï f(x)‚ü© = |y‚ü©.
        // This means that the operation doesn't need to do any transformation to the inputs.
        // Build the project and run the tests to see that T01_Oracle_Zero test passes.
    }
    
    
    // Task 1.2. f(x) = 1
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    operation Oracle_One_Reference (x : Qubit[], y : Qubit) : Unit is Adj {
        // Since f(x) = 1 for all values of x, |y ‚äï f(x)‚ü© = |y ‚äï 1‚ü© = |NOT y‚ü©.
        // This means that the operation needs to flip qubit y (i.e. transform |0‚ü© to |1‚ü© and vice versa).
        X(y);
    }
    
    
    // Task 1.3. f(x) = x‚Çñ (the value of k-th qubit)
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    //      3) 0-based index of the qubit from input register (0 <= k < N)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï x‚Çñ‚ü© (‚äï is addition modulo 2).
    operation Oracle_Kth_Qubit_Reference (x : Qubit[], y : Qubit, k : Int) : Unit is Adj {        
        EqualityFactB(0 <= k and k < Length(x), true, "k should be between 0 and N-1, inclusive");
        CNOT(x[k], y);
    }
    
    
    // Task 1.4. f(x) = 1 if x has odd number of 1s, and 0 otherwise
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    operation Oracle_OddNumberOfOnes_Reference (x : Qubit[], y : Qubit) : Unit is Adj {       
        // Hint: f(x) can be represented as x_0 ‚äï x_1 ‚äï ... ‚äï x_(N-1)
        for q in x {
            CNOT(q, y);
        }
        // alternative solution: ApplyToEachA(CNOT(_, y), x);
    }
    
    
    // Task 1.5. f(x) = Œ£·µ¢ ùëü·µ¢ ùë•·µ¢ modulo 2 for a given bit vector r (scalar product function)
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    //      3) a bit vector of length N represented as Int[]
    // You are guaranteed that the qubit array and the bit vector have the same length.
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    
    // Note: the functions featured in tasks 1.1, 1.3 and 1.4 are special cases of this function.
    operation Oracle_ProductFunction_Reference (x : Qubit[], y : Qubit, r : Int[]) : Unit is Adj {        
        // The following line enforces the constraint on the input arrays.
        // You don't need to modify it. Feel free to remove it, this won't cause your code to fail.
        EqualityFactI(Length(x), Length(r), "Arrays should have the same length");
            
        for i in IndexRange(x) {
            if (r[i] == 1) {
                CNOT(x[i], y);
            }
        }
    }
    
    
    // Task 1.6. f(x) = Œ£·µ¢ (ùëü·µ¢ ùë•·µ¢ + (1 - ùëü·µ¢)(1 - ùë•·µ¢)) modulo 2 for a given bit vector r
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    //      3) a bit vector of length N represented as Int[]
    // You are guaranteed that the qubit array and the bit vector have the same length.
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    operation Oracle_ProductWithNegationFunction_Reference (x : Qubit[], y : Qubit, r : Int[]) : Unit is Adj {
        // The following line enforces the constraint on the input arrays.
        // You don't need to modify it. Feel free to remove it, this won't cause your code to fail.
        EqualityFactI(Length(x), Length(r), "Arrays should have the same length");
            
        for i in IndexRange(x) {
            if (r[i] == 1) {
                CNOT(x[i], y);
            } else {
                // do a 0-controlled NOT
                (ControlledOnInt(0, X))([x[i]], y);
            }
        }
    }
    
    
    // Task 1.7. f(x) = Œ£·µ¢ ùë•·µ¢ + (1 if prefix of x is equal to the given bit vector, and 0 otherwise) modulo 2
    // Inputs:
    //      1) N qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    //      3) a bit vector of length P represented as Int[] (1 <= P <= N)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    
    // A prefix of length k of a state |x‚ü© = |x‚ÇÅ, ..., x‚Çô‚ü© is the state of its first k qubits |x‚ÇÅ, ..., x‚Çñ‚ü©.
    // For example, a prefix of length 2 of a state |0110‚ü© is 01.
    operation Oracle_HammingWithPrefix_Reference (x : Qubit[], y : Qubit, prefix : Int[]) : Unit is Adj {        
        // The following line enforces the constraint on the input arrays.
        // You don't need to modify it. Feel free to remove it, this won't cause your code to fail.
        let P = Length(prefix);
        EqualityFactB(1 <= P and P <= Length(x), true, "P should be between 1 and N, inclusive");
            
        // Hint: the first part of the function is the same as in task 1.4
        for q in x {
            CNOT(q, y);
        }
            
        // add check for prefix as a multi-controlled NOT
        // true bits of r correspond to 1-controls, false bits - to 0-controls
        within {
            for i in 0 .. P - 1 {
                    
                if (prefix[i] == 0) {
                    X(x[i]);
                }
            }
        } apply {
            Controlled X(x[0 .. P - 1], y);
        }
    }
    
    
    // Task 1.8*. f(x) = 1 if x has two or three bits (out of three) set to 1, and 0 otherwise  (majority function)
    // Inputs:
    //      1) 3 qubits in arbitrary state |x‚ü© (input register)
    //      2) a qubit in arbitrary state |y‚ü© (output qubit)
    // Goal: transform state |x, y‚ü© into state |x, y ‚äï f(x)‚ü© (‚äï is addition modulo 2).
    operation Oracle_MajorityFunction_Reference (x : Qubit[], y : Qubit) : Unit is Adj {        
        // The following line enforces the constraint on the input array.
        // You don't need to modify it. Feel free to remove it, this won't cause your code to fail.
        EqualityFactB(3 == Length(x), true, "x should have exactly 3 qubits");
            
        // f(x) can be represented in terms of AND and ‚äï operations as follows:
        // f(x) = (x‚ÇÄ AND x‚ÇÅ) ‚äï (x‚ÇÄ AND x‚ÇÇ) ‚äï (x‚ÇÅ AND x‚ÇÇ)
        CCNOT(x[0], x[1], y);
        CCNOT(x[0], x[2], y);
        CCNOT(x[1], x[2], y);
    }  
    

    //////////////////////////////////////////////////////////////////
    // Part II. Deutsch-Jozsa Algorithm
    //////////////////////////////////////////////////////////////////

    // Task 2.1. State preparation for Deutsch-Jozsa (or Bernstein-Vazirani) algorithm
    // Inputs:
    //      1) N qubits in |0‚ü© state (query register)
    //      2) a qubit in |0‚ü© state (answer register)
    // Goal:
    //      1) prepare an equal superposition of all basis vectors from |0...0‚ü© to |1...1‚ü© on query register
    //         (i.e. state (|0...0‚ü© + ... + |1...1‚ü©) / sqrt(2^N) )
    //      2) prepare |-‚ü© state (|-‚ü© = (|0‚ü© - |1‚ü©) / sqrt(2)) on answer register
    operation DJ_StatePrep_Reference (query : Qubit[], answer : Qubit) : Unit is Adj {        
        ApplyToEachA(H, query);
        X(answer);
        H(answer);
    }
    
    // Task 2.2. Deutsch-Jozsa algorithm implementation
    // Inputs:
    //      1) the number of qubits in the input register N for the function f
    //      2) a quantum operation which implements the oracle |x‚ü©|y‚ü© -> |x‚ü©|y ‚äï f(x)‚ü©, where
    //         x is N-qubit input register, y is 1-qubit answer register, and f is a Boolean function
    // You are guaranteed that the function f implemented by the oracle is either
    // constant (returns 0 on all inputs or 1 on all inputs) or
    // balanced (returns 0 on exactly one half of the input domain and 1 on the other half).
    // Output:
    //      true if the function f is constant
    //      false if the function f is balanced
    
    // Note: a trivial approach is to call the oracle multiple times:
    //       if the values for more than half of the possible inputs are the same, the function is constant.
    // Quantum computing allows to perform this task in just one call to the oracle; try to implement this algorithm.
    operation DJ_Algorithm_Reference (N : Int, Uf : ((Qubit[], Qubit) => Unit)) : Bool {
        
        // Declare variable in which the result will be accumulated;
        // this variable has to be mutable to allow updating it.
        mutable isConstantFunction = true;
    
        // allocate N qubits for input register and 1 qubit for output
        use (x, y) = (Qubit[N], Qubit());

        // prepare qubits in the right state
        DJ_StatePrep_Reference(x, y);

        // apply oracle
        Uf(x, y);
            
        // apply Hadamard to each qubit of the input register
        ApplyToEach(H, x);
            
        // measure all qubits of the input register;
        // the result of each measurement is converted to an Int
        for i in 0 .. N - 1 {
            if (M(x[i]) != Zero) {
                set isConstantFunction = false;
            }
        }
            
        // before releasing the qubits make sure they are all in |0‚ü© state
        Reset(y);
        
        return isConstantFunction;
    }


    //////////////////////////////////////////////////////////////////
    // Part III. Bernstein-Vazirani Algorithm
    //////////////////////////////////////////////////////////////////  
    
    // Task 3.1. Bernstein-Vazirani algorithm implementation
    // Inputs:
    //      1) the number of qubits in the input register N for the function f
    //      2) a quantum operation which implements the oracle |x‚ü©|y‚ü© -> |x‚ü©|y ‚äï f(x)‚ü©, where
    //         x is N-qubit input register, y is 1-qubit answer register, and f is a Boolean function
    // You are guaranteed that the function f implemented by the oracle is a scalar product function
    // (can be represented as f(ùë•‚ÇÄ, ‚Ä¶, ùë•‚Çô‚Çã‚ÇÅ) = Œ£·µ¢ ùëü·µ¢ ùë•·µ¢ modulo 2 for some bit vector r = (ùëü‚ÇÄ, ‚Ä¶, ùëü‚Çô‚Çã‚ÇÅ)).
    // You have implemented the oracle implementing the scalar product function in task 1.5.
    // Output:
    //      A bit vector r reconstructed from the function
    
    // Note: a trivial approach is to call the oracle N times:
    //       |10...0‚ü©|0‚ü© = |10...0‚ü©|r‚ÇÄ‚ü©, |010...0‚ü©|0‚ü© = |010...0‚ü©|r‚ÇÅ‚ü© and so on.
    // Quantum computing allows to perform this task in just one call to the oracle; try to implement this algorithm.
    operation BV_Algorithm_Reference (N : Int, Uf : ((Qubit[], Qubit) => Unit)) : Int[] {
                
        // allocate N qubits for input register and 1 qubit for output
        use (x, y) = (Qubit[N], Qubit());
            
        // prepare qubits in the right state
        DJ_StatePrep_Reference(x, y);
            
        // apply oracle
        Uf(x, y);
            
        // apply Hadamard to each qubit of the input register
        ApplyToEach(H, x);
            
        // measure all qubits of the input register;
        // the result of each measurement is converted to an Int
        mutable r = new Int[N];
        for i in 0 .. N - 1 {
            if (M(x[i]) != Zero) {
                set r w/= i <- 1;
            }
        }
            
        // before releasing the qubits make sure they are all in |0‚ü© state
        Reset(y);
        return r;
    }
    
    
    //////////////////////////////////////////////////////////////////
    // Part IV. Come up with your own algorithm!
    //////////////////////////////////////////////////////////////////
    
    // Task 4.1. Reconstruct the oracle from task 1.6
    // Inputs:
    //      1) the number of qubits in the input register N for the function f
    //      2) a quantum operation which implements the oracle |x‚ü©|y‚ü© -> |x‚ü©|y ‚äï f(x)‚ü©, where
    //         x is N-qubit input register, y is 1-qubit answer register, and f is a Boolean function
    // You are guaranteed that the function f implemented by the oracle can be represented as
    // f(ùë•‚ÇÄ, ‚Ä¶, ùë•‚Çô‚Çã‚ÇÅ) = Œ£·µ¢ (ùëü·µ¢ ùë•·µ¢ + (1 - ùëü·µ¢)(1 - ùë•·µ¢)) modulo 2 for some bit vector r = (ùëü‚ÇÄ, ‚Ä¶, ùëü‚Çô‚Çã‚ÇÅ).
    // You have implemented the oracle implementing this function in task 1.6.
    // Output:
    //      A bit vector r which generates the same oracle as the one you are given
    operation Noname_Algorithm_Reference (N : Int, Uf : ((Qubit[], Qubit) => Unit)) : Int[] {
                
        use (x, y) = (Qubit[N], Qubit());
        // apply oracle to qubits in all 0 state
        Uf(x, y);
            
        // f(x) = Œ£·µ¢ (ùëü·µ¢ ùë•·µ¢ + (1 - ùëü·µ¢)(1 - ùë•·µ¢)) = 2 Œ£·µ¢ ùëü·µ¢ ùë•·µ¢ + Œ£·µ¢ ùëü·µ¢ + Œ£·µ¢ ùë•·µ¢ + N = Œ£·µ¢ ùëü·µ¢ + N
        // remove the N from the expression
        if (N % 2 == 1) {
            X(y);
        }
            
        // now y = Œ£·µ¢ ùëü·µ¢
            
        // Declare an Int array in which the result will be stored;
        // the variable has to be mutable to allow updating it.
        mutable r = new Int[N];

        // measure the output register
        let m = M(y);
        if (m == One) {
            // adjust parity of bit vector r
            set r w/= 0 <- 1;
        }
            
        return r;
    }
    
}
